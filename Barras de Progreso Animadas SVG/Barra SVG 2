Barra SVG 2 = 
VAR _Progreso = SUM('Tabla Vendedor'[Progreso])
VAR _AnchoTotal = 300
VAR _AltoTotal = 70
VAR _RadioEsquinas = 20
VAR _Margen = 10

VAR _AnchoMax = _AnchoTotal - 2 * _Margen
VAR _AnchoBarra = MAX(0, MIN(_AnchoMax, _AnchoMax * _Progreso))

VAR _AnchoTexto = 80
VAR _AltoTexto = 40

VAR svg = 
    "<svg xmlns='http://www.w3.org/2000/svg' width='" & 
    FORMAT(_AnchoTotal, "0") & "' height='" & FORMAT(_AltoTotal, "0") & "'>" & 

    "<defs>" &
        "<linearGradient id='grad'>" & 
            "<stop offset='0%' stop-color='#6A0DAD'/>" & 
            "<stop offset='100%' stop-color='#FF1493'/>" & 
        "</linearGradient>" &
    "</defs>" & 

    "<rect x='" & FORMAT(_Margen, "0") & "' y='" & FORMAT(_Margen, "0") & "' width='" & 
    FORMAT(_AnchoMax, "0") & "' height='" & FORMAT(_AltoTotal - 2 * _Margen, "0") & 
    "' rx='" & FORMAT(_RadioEsquinas, "0") & "' ry='" & FORMAT(_RadioEsquinas, "0") & "' fill='#E0E0E0'/>" & 

    "<rect x='" & FORMAT(_Margen, "0") & "' y='" & FORMAT(_Margen, "0") & "' width='0' height='" & 
    FORMAT(_AltoTotal - 2 * _Margen, "0") & "' rx='" & FORMAT(_RadioEsquinas, "0") & "' ry='" & FORMAT(_RadioEsquinas, "0") & 
    "' fill='url(#grad)'>" &
        "<animate attributeName='width' from='0' to='" & FORMAT(_AnchoBarra, "0") & "' dur='2s' fill='freeze' />" &
    "</rect>" &

    "<rect x='" & FORMAT((_AnchoTotal - _AnchoTexto) / 2, "0") & "' y='" & FORMAT((_AltoTotal - _AltoTexto) / 2, "0") & 
    "' width='" & FORMAT(_AnchoTexto, "0") & "' height='" & FORMAT(_AltoTexto, "0") & 
    "' rx='10' ry='10' fill='white' fill-opacity='0.8'/>" & 

    "<text x='" & FORMAT(_AnchoTotal / 2, "0") & "' y='" & FORMAT(_AltoTotal / 1.5, "0") & 
    "' font-family='Calibri' font-size='28' fill='black' text-anchor='middle'>" & 
    FORMAT(_Progreso * 100, "0") & "%</text>" & 

    "</svg>"

RETURN
"data:image/svg+xml;utf8," & svg
