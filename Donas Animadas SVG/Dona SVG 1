Dona SVG 1 = 
VAR TasaCumplimiento = [% Tickets Resueltos]
VAR Porcentaje = MIN(TasaCumplimiento, 1)
VAR PorcentajeTexto = FORMAT(TasaCumplimiento, "0%")
VAR Radio = 80
VAR Centro = 100
VAR GrosorBorde = 30
VAR Circunferencia = 2 * PI() * Radio
VAR CircunferenciaTexto = FORMAT(Circunferencia, "0")
VAR CircunferenciaFaltante = FORMAT(Circunferencia * (1 - Porcentaje), "0")

RETURN
"data:image/svg+xml;utf8," &
"<svg width='200' height='200' viewBox='0 0 200 200' xmlns='http://www.w3.org/2000/svg'>" &

  "<defs>" &
    "<linearGradient id='gradienteCircular' x1='0%' y1='0%' x2='0%' y2='100%'>" &
      "<stop offset='0%' stop-color='#3333ff'/>" &
      "<stop offset='100%' stop-color='#ff00cc'/>" &
    "</linearGradient>" &
  "</defs>" &

  "<circle cx='" & Centro & "' cy='" & Centro & "' r='" & Radio & "' " &
    "stroke='#e0e0e0' stroke-width='" & GrosorBorde & "' fill='none'/>" &

  "<circle cx='" & Centro & "' cy='" & Centro & "' r='" & Radio & "' " &
    "stroke='url(#gradienteCircular)' stroke-width='" & GrosorBorde & "' fill='none' " &
    "stroke-dasharray='" & CircunferenciaTexto & "' " &
    "stroke-dashoffset='" & CircunferenciaTexto & "' " &
    "stroke-linecap='round' transform='rotate(-90 " & Centro & " " & Centro & ")'>" &
      "<animate attributeName='stroke-dashoffset' from='" & CircunferenciaTexto & "' to='" & CircunferenciaFaltante & "' dur='1.5s' fill='freeze' />" &
  "</circle>" &

  "<text x='100' y='110' font-family='Calibri' font-size='20' text-anchor='middle' fill='#000000'>" &
    PorcentajeTexto &
  "</text>" &

"</svg>"